plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id 'maven'
    id 'signing'
    id 'net.researchgate.release' version '2.6.0'
    id 'io.codearte.nexus-staging' version '0.20.0'
}

group 'com.github.simkuenzi'
version version

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
    useGpgCmd()
}

uploadArchives {
    dependsOn = [jar, sourceJar, javadocJar]
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: mavenUser, password: mavenPassword)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: mavenUser, password: mavenPassword)
            }
            pom.project {
                packaging 'jar'
                name "$project.group:$project.name"
                description 'A libray which helps to structure parsing and validation of URL encoded HTTP forms.'
                url 'https://github.com/simkuenzi/restforms'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name 'Simon KÃ¼nzi'
                        email 'sim.kuenzi@gmail.com'
                        organizationUrl 'https://github.com/simkuenzi'
                    }
                }
                scm {
                    connection 'scm:git:https://github.com/simkuenzi/restforms/restforms.git'
                    developerConnection 'scm:git:https://github.com/simkuenzi/restforms/restforms.git'
                    url 'https://github.com/simkuenzi/restforms'
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives, closeAndReleaseRepository
closeAndReleaseRepository.mustRunAfter uploadArchives